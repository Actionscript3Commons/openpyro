package org.openPyro.aurora
{
	import com.cimians.openPyro.controls.Button;
	import com.cimians.openPyro.controls.events.ButtonEvent;
	import com.cimians.openPyro.core.UIControl;
	import com.cimians.openPyro.events.PyroEvent;
	import com.cimians.openPyro.painters.GradientFillPainter;
	import com.cimians.openPyro.painters.Stroke;
	
	import flash.display.DisplayObject;
	import flash.display.Sprite;
	
	public class AuroraCheckBoxSkin extends AuroraPainterButtonSkin
	{
		
		[Embed(source="/assets/graphic_assets.swf", symbol="checkIcon")]
		private var TickGraphic:Class
		
		public function AuroraCheckBoxSkin()
		{
		}
		
		private var _checkIcon:DisplayObject
		
		public var cornerRadius:Number = 0
		public var boxLabelGap:Number = 10;
		
		public function set checkIcon(icon:DisplayObject):void{
			_checkIcon = icon
		}
		
		private var _uncheckIcon:DisplayObject
		public function set uncheckIcon(icon:DisplayObject):void{
			_uncheckIcon = icon;
		}
		
		override public function set skinnedControl(uic:UIControl):void
		{
			super.skinnedControl = uic
			checkSelectedStatus()
		}
		
		override protected function onSkinnedControlPropertyChange(event:PyroEvent):void{
			super.onSkinnedControlPropertyChange(event);
			checkSelectedStatus()
		}
		
		override public function changeState(fromState:String, toState:String):void
		{
			if(toState==ButtonEvent.UP)
			{
				this.backgroundPainter = upPainter;
			}
			
			else if(toState==ButtonEvent.OVER)
			{
				this.backgroundPainter = overPainter;
			}
			
			else if(toState == ButtonEvent.DOWN)
			{
				this.backgroundPainter = downPainter;
				if(_skinnedControl is Button){
					var b:Button = _skinnedControl as Button;
					checkSelectedStatus()
				}
			}
			else
			{
				this.backgroundPainter = upPainter;
			}
		}
		
		protected function checkSelectedStatus():void{
			if(Button(_skinnedControl).toggle){
				if(Button(_skinnedControl).selected){
					if(!_checkIcon){
						_checkIcon = createDefaultCheckIcon()
						
					}
					_checkIcon.visible = true
					if(_uncheckIcon){
						_uncheckIcon.visible = false;
					}	
				}
				else {
					if(!_uncheckIcon){
						_uncheckIcon = createDefaultUnCheckIcon()
						
					}
					_uncheckIcon.visible = true
					if(_checkIcon){
						_checkIcon.visible=false
					}
				}
			}
		}
		
		override public function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void{
			super.updateDisplayList(unscaledWidth, unscaledHeight);
				if(!label){
					if(_checkIcon)
						_checkIcon.x = (unscaledWidth-_checkIcon.width)/2;
					if(_uncheckIcon)
						_uncheckIcon.x = 	(unscaledWidth-_uncheckIcon.width)/2
				}
				else{
						if(_checkIcon){
							_checkIcon.x = _skinnedControl.padding.left;
							_checkIcon.y = (unscaledHeight-_checkIcon.height)/2;
			
						}
						if(_uncheckIcon){
							_uncheckIcon.x =  _skinnedControl.padding.left;;
							_uncheckIcon.y = (unscaledHeight-_uncheckIcon.height)/2;
						}
						var checkIconW:Number = _checkIcon ? _checkIcon.width:0
						var uncheckIconW:Number = _uncheckIcon?_uncheckIcon.width:0
						label.x = Math.max(checkIconW, uncheckIconW)+boxLabelGap;
					}
			
		}
		
		protected function createDefaultUnCheckIcon():Sprite{
			var sp:Sprite = new Sprite()
			var gr:GradientFillPainter = new GradientFillPainter([0xffffff, 0xdddddd])
			gr.stroke = new Stroke(1,0x666666,1,true)
			gr.rotation = Math.PI/2
			gr.cornerRadius = cornerRadius
			gr.draw(sp.graphics, 15,15)
			addChild(sp)
			return sp
		}
		
		protected function createDefaultCheckIcon():Sprite{
			var sp:Sprite = new Sprite()
			var gr:GradientFillPainter = new GradientFillPainter([0xffffff, 0xdddddd])
			gr.stroke = new Stroke(1,0x666666,1,true)
			gr.rotation = Math.PI/2
			gr.cornerRadius = cornerRadius
			gr.draw(sp.graphics, 15,15)
			addChild(sp)
			
			var tick:DisplayObject = new TickGraphic()
			sp.addChild(tick)
			sp.mouseChildren=false;
			return sp
		}
		

	}
}